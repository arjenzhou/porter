group 'de.0xab'
version project.property('porter-version')

ext {
    //plugin version
    checkstylePlugin = '8.44'
    //test dependencies version
    jupiter = '5.7.0'
    mockito = '3.11.2'
    h2 = '1.4.200'
    //dependencies version
    jackson = '2.12.4'
    hikari = '4.0.3'
    clickhouse = '0.3.1-patch'
    mysql = '8.0.25'
    mariadb = '3.0.0-alpha'
    postgresql = '42.2.23'
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply from: rootDir.path + '/gradle/checkstyle.gradle'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
}

repositories {
    mavenCentral()
}

subprojects {
    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:' + property('jupiter')
        testImplementation 'org.mockito:mockito-core:' + property('mockito')
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:' + property('jupiter')
    }
    test.useJUnitPlatform()
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}