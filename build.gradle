group 'de.0xab'
version project.property('porter-version')

ext {
    gradleWrapper = '7.1.1'
    //plugin version
    checkstylePlugin = '8.44'

    //test dependencies version
    jupiter = '5.7.0'
    mockito = '3.11.2'

    //dependencies version
    //- demo
    h2 = '1.4.200'
    //- common
    jackson = '2.12.4'
    cglib = '3.3.0'
    //- transfer
    // - jdbc
    hikari = '5.0.0'
    clickhouse = '0.3.1-patch'
    mysql = '8.0.25'
    mariadb = '2.7.3'
    postgresql = '42.2.23'
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply from: rootDir.path + '/codestyle/checkstyle.gradle'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
}

repositories {
    mavenCentral()
}

subprojects {
    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiter"
        testImplementation "org.mockito:mockito-core:$mockito"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiter"
    }
    test.useJUnitPlatform()
}

wrapper {
    gradleVersion = "$gradleWrapper"
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.required.set true
        html.required.set false
        csv.required.set false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}